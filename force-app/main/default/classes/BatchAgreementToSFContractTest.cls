/*------------------------------------------------------------------------
Author:         Melissa Carino
Description:    Test class for BatchAgreementToSFContract class
History
<Date>        <Authors Name>      <Brief Description of Change>
17/09/2018    Melissa Carino    First version.
----------------------------------------------------------------------------*/
@isTest 
public class BatchAgreementToSFContractTest{
    /*------------------------------------------------------------
    Author:        Melissa Carino
    Company:       Accenture
    Description:   Setup data to create 20 test Apttus contract for batch cloning to sf contract
    History
    <Date>      <Authors Name>     <Brief Description of Change>
    22-Oct-2018  Melissa Carino     Created setup data initial version
    ------------------------------------------------------------*/
    @testSetup static void setup() {
        // Create test organisation
        Account accVar = APT_TestUtil2.createOrganisation();
        insert accVar;

        // Create test contact
        Contact conVar = APT_TestUtil2.createContact();
        conVar.AccountId = accVar.Id;
        insert conVar;

        Integer i = 0;

        // Create test opportunity
        List<Opportunity> oppList = new List<Opportunity>();
        Map<Integer, Opportunity> oppMap = new Map<Integer, Opportunity>();
        for(i=0;i<20;i++){
            Opportunity oppVar = APT_TestUtil2.createOpportunity(accVar);
            oppList.add(oppVar);
        }

        insert oppList;

        // Create test agreement
        List<Apttus__APTS_Agreement__c> testAgreementList = new List<Apttus__APTS_Agreement__c>();
        for(i=0;i<20;i++) {
            Apttus__APTS_Agreement__c aptContract = APT_TestUtil2.createAgreement(accVar,
                                                                                  conVar,
                                                                                  oppList[i],
                                                                                  new Apttus_Proposal__Proposal__c(),
                                                                                  BatchAgreementToSFContract.ACTIVATED_STATUS,
                                                                                  BatchAgreementToSFContract.INEFFECT_STATUS_CATEGORY,
                                                                                  'MSA & Service Schedule');
            testAgreementList.add(aptContract);
        }
        insert testAgreementList;

        // Code below will Flush clone SF Contracts generated by insert.
        // Insert triggers an update chain causing the clones to be populated via agreement trigger.
        // For production, this event will not occur since the trigger is not yet implemented for historical In Effect - Activated contracts.
        System.assertEquals(20 , [Select Count() from contract]); // Check that clones are created
        List<Contract> contractList = [Select id , Status from contract];
        delete contractList;
    }

    /*------------------------------------------------------------
    Author:        Melissa Carino
    Company:       Accenture
    Description:   Scenario: Test apttus contract is created and batch job executes.
                   Outcome: Clone SF Contracts are generated and email notification is sent that batch completed.
    History
    <Date>      <Authors Name>     <Brief Description of Change>
    22-Oct-2018  Melissa Carino     Initial version
    ------------------------------------------------------------*/
    static testMethod void validateContractMassClone() {
        //Added by Melissa for Email Testing 
        system.assertEquals(0, BatchAgreementToSFContract.emailLimits); 
        Test.startTest();
        // Validate that there are no clone SF Contracts before calling batch
        System.assertEquals(0 , [Select Count() from contract]); //Intitially No Contract
        // Call the batch to mass clone contracts
        Database.executeBatch(new BatchAgreementToSFContract(), 200);
        Test.stopTest();
        // Check that clone contracts are created
        System.assertEquals(20 , [Select Count() from contract]);
        //Test that email notifcation is sent on batch finish
        system.assertEquals(1, BatchAgreementToSFContract.emailLimits); 
    }

}